breed [ points point ]  ;; the points of the search space function
breed [ particles particle ]  ;; agents (turtles) that move around the space

globals [
  g-best-x
  g-best-y
  g-best-val
  true-best-patch
]

particles-own [
  vx
  vy
  p-best-x
  p-best-y
  p-best-val    ; best value of the particle
]

patches-own [
  elevation     ; fitness value associated to each patch
]


;; We evaluate the given points with the function
to-report objective-function [x y]

  ;; Sphere function
  let value  (x ^ 2 + y ^ 2)
  ;let value (-20 * exp(-0.2 * sqrt(0.5 * (x * x + y * y))) - exp(0.5 * (cos (2 * pi * x)) + (cos (2 * pi * y))) + e + 20)
  report value
end


to setup

  clear-all
  ask patches [set elevation 0]
  ifelse random-search-space
    [
      create-random-search-space
    ]
  [
    ; Calculate the objective function to each pair of coordinates of patches
    ask patches with [pzcor = 0] [set elevation (objective-function pxcor pycor)]
  ]

  draw-search-space
  initialize-particles
  set-observer-position

  reset-ticks
end


to set-observer-position

  ifelse random-search-space [
    ask patches with [pzcor = max-pzcor] [set pcolor 102]
    setxyz -40 -40 -50
    facexyz -0 -0 10
    orbit-down 100
  ]
  [
    ;ask patches with [pzcor = 0] [set pcolor 102]
    setxyz 50 50 50
    facexyz -0 -0 10
  ]
end

to create-random-search-space

  ask n-of hills-number patches with [pzcor = 0 ] [ set elevation 50 + random 100 ]  ; 50-150 of elevation
  ; To smooth the landscape
  repeat landscape-smoothness [ diffuse elevation 1]

  ask max-one-of patches [elevation]
  [
    set true-best-patch self
  ]
end


;; turtle procedure to get the fitness (elevation)
to-report get-elevation

  ifelse random-search-space
    ; We aproximate the coordinates of actual particle because we don't know
    ; the fitness function, but we know the given random elevation
    [report [elevation] of (patch (int xcor) (int ycor) 0)]

  ; In case we don't use random search space, we report the exactly fitness value
  [report objective-function xcor ycor]
end


to draw-search-space

  ; The maximum value of the objective function
  let max-elevation max [elevation] of patches

  ; We create the points over the landscape to link them
  ask patches with [pzcor = 0]
  [
    sprout-points 1
    [
      create-links-with points-on neighbors
      hide-turtle
    ]
  ]

  ask points
  [
    ; We draw the search space proportional to sizes of world
    set zcor (elevation / max-elevation) * max-pzcor
  ]
end


to initialize-particles

  ; Set particles at random locations
  create-particles population-size [

    ; particles have random initial coordinates and velocities
    setxy random-xcor random-ycor

    ; the starting point is the best particle's location
    set p-best-x xcor
    set p-best-y ycor

    let z-elevation get-elevation
    let max-elevation max [elevation] of patches
    set zcor (z-elevation / max-elevation) * max-pzcor

    set p-best-val z-elevation

    set vx random-normal 0 1
    set vy random-normal 0 1

    ; aspect of particles
    set color red
    set shape "dot"
    set size 2
  ]
end


to update-personal-global-best

  ask particles [

  ; update the "personal best" location
    let current-elevation get-elevation

    if (random-search-space and current-elevation > p-best-val) or (current-elevation < p-best-val) [
      set p-best-val current-elevation
      set p-best-x xcor
      set p-best-y ycor ]
  ]
  ; update the "global best" location
  ifelse random-search-space [
    ask max-one-of particles [p-best-val] [
      if p-best-val > g-best-val [
        set g-best-val p-best-val
        set g-best-x p-best-x
        set g-best-y p-best-y ]]
  ]
  [
    ask min-one-of particles [p-best-val] [
      if p-best-val < g-best-val [
        set g-best-val p-best-val
        set g-best-x p-best-x
        set g-best-y p-best-y ]]
  ]
end


;; Optimize through PSO
to go

  update-personal-global-best

  ; update the velocities and positions of each particle
  ask particles
  [

    ifelse trace-mode [ pen-down ] [ pen-up ]

    set vx particle-inertia * vx
    set vy particle-inertia * vy

    ; change my velocity by being attracted to the "personal best" value
    facexy p-best-x p-best-y

    let dist distancexy p-best-x p-best-y
    set vx vx + (1 - particle-inertia) * attraction-to-personal-best * (random-float 1.0) * dist * dx
    set vy vy + (1 - particle-inertia) * attraction-to-personal-best * (random-float 1.0) * dist * dy

    ; change my velocity by being attracted to the "global best" value anyone has found so far
    facexy g-best-x g-best-y
    set dist distancexy g-best-x g-best-y
    set vx vx + (1 - particle-inertia) * attraction-to-global-best * (random-float 1.0) * dist * dx
    set vy vy + (1 - particle-inertia) * attraction-to-global-best * (random-float 1.0) * dist * dy

    ; speed limits are particularly necessary because we are dealing with a toroidal (wrapping) world,
    ; which means that particles can start warping around the world at ridiculous speeds
    if (vx > particle-speed-limit) [ set vx particle-speed-limit ]
    if (vx < 0 - particle-speed-limit) [ set vx 0 - particle-speed-limit ]
    if (vy > particle-speed-limit) [ set vy particle-speed-limit ]
    if (vy < 0 - particle-speed-limit) [ set vy 0 - particle-speed-limit ]

    ; face in the direction of my velocity
    facexy (xcor + vx) (ycor + vy)

    ; and move forward by the magnitude of my velocity
    forward sqrt (vx * vx + vy * vy)

    ; set the zcor at visualization
    let z-elevation get-elevation
    let max-elevation max [elevation] of patches
    set zcor (z-elevation / max-elevation) * max-pzcor
  ]

  if not trace-mode [ clear-drawing ]
  tick
end
@#$#@#$#@
GRAPHICS-WINDOW
0
0
254
255
-1
-1
6.0
1
10
1
1
1
0
1
1
1
-20
20
-20
20
0
20
1
1
1
ticks
30.0

BUTTON
241
65
343
112
NIL
setup
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

BUTTON
240
174
343
218
NIL
go
T
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

SWITCH
26
71
201
104
random-search-space
random-search-space
0
1
-1000

BUTTON
241
120
343
166
step
go
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

SLIDER
381
48
555
81
population-size
population-size
0
100
13.0
1
1
NIL
HORIZONTAL

SLIDER
381
102
553
135
particle-inertia
particle-inertia
0
1
0.9
0.1
1
NIL
HORIZONTAL

SLIDER
382
142
554
175
particle-speed-limit
particle-speed-limit
0
20
10.0
1
1
NIL
HORIZONTAL

SLIDER
383
196
584
229
attraction-to-personal-best
attraction-to-personal-best
0
2
1.9
0.1
1
NIL
HORIZONTAL

SLIDER
383
234
583
267
attraction-to-global-best
attraction-to-global-best
0
2
1.5
0.1
1
NIL
HORIZONTAL

SLIDER
26
121
202
154
landscape-smoothness
landscape-smoothness
4
20
10.0
1
1
NIL
HORIZONTAL

SLIDER
26
167
204
200
hills-number
hills-number
1
50
30.0
1
1
NIL
HORIZONTAL

PLOT
138
302
455
502
plot 1
Generacion
fitness
0.0
10.0
0.0
10.0
true
true
"" ""
PENS
"mean" 1.0 0 -10899396 true "" "plot mean [p-best-val] of particles"
"max" 1.0 0 -2674135 true "" "plot max [p-best-val] of particles"
"min" 1.0 0 -13791810 true "" "plot min [p-best-val] of particles"

SWITCH
26
228
204
261
trace-mode
trace-mode
1
1
-1000

@#$#@#$#@
## ¿ QUÉ ES ?

La optimización del enjambre de partículas, del inglés 'Particle Swarm Optimization' (PSO) es una técnica / método computacional en el campo del aprendizaje automático que optimiza un problema mediante partículas que se mueven en un espacio de búsqueda de acuerdo con una fórmula matemática sobre la posición y velocidad de la partícula.

Por lo general, existe una función desconocida llamada función de fitness o "fitness landscape" que determina qué tan buena es la posición actual en el espacio para cada partícula. El método PSO intenta minimizar esta función de aptitud.

Algunas alteraciones fueron necesarias para tener en cuenta el uso de un mundo toroidal (envolvente) y para mejorar la visualización del movimiento del enjambre. Además, la función que se optimiza es discreta (basada en una cuadrícula de valores), en lugar de continua.

## COMO FUNCIONA

Las partículas se definen por su posición (x, y) en el paisaje de búsqueda y su velocidad (vx, vy) con la que se mueven a través de ese paisaje. Las partículas también tienen una cantidad de inercia que las mantiene moviéndose en la misma dirección en la que se movían anteriormente.

Cada partícula tiene aceleración, que depende de dos factores:

- La mejor ubicación que ha encontrado personalmente (p-mejor) antes en su historia. Este factor se conoce como "atracción por la mejor puntuación personal".
- La mejor ubicación que haya encontrado una partícula (g-best) en el espacio de búsqueda. Este factor se conoce como 'atracción-al-mejor-global'.

También hay un factor aleatorio sobre cuánto se tira de la partícula hacia cada una de estas ubicaciones.

**Algoritmo** (pseudocódigo)

```
Para cada partículo (i=1, ... , n) do
    Inicializar la posición de la particula de forma uniform. distribuida
    Inicializar la mejor posición local encontrada de la partícula (p-best)
        Actualizar la mejor global (g-best) si p-best es mejor que ésta
	Inicializamos la velocidad de la partícula
while n-evaluations <= max-evaluations do:
	Para cada partícula (i=1, ... , n) do
		Para cada dimensión (i=1, ..., d) do
			Actualizamos la velocidad de la partícula
		Actualizamos la posición de la partícula
		Actualizamos el mejor local (p-best) si el actual es mejor
			Actualizamos g-best si p-best es mejor
```

En este modelo, el enjambre de partículas está tratando de minimizar una función de esfera que está determinada por los valores en la cuadrícula discreta de celdas que se muestra en la vista del modelo 3D.

La opción que permite crear un espacio de búsqueda aleatorio funciona asignando valores a cada celda de la cuadrícula, luego realizando una difusión para suavizar los valores, resultando en numerosos máximos (colinas) y mínimos locales (valles).

Para ilustrar cómo funciona esta técnica, el modelo se ejecuta hasta que el usuario considera necesario detenerlo. Permite cambiar los valores de diferentes parámetros mientras se ejecuta el modelo.


## CÓMO USARLO

Presione SETUP para inicializar el paisaje de fitness y colocar las partículas al azar en el espacio de la función de esfera. f (x, y) = x <sup>2</sup> + y <sup>2</sup>

Si se selecciona el interruptor 'espacio de búsqueda aleatorio', cada vez que presione SETUP, se creará un paisaje aleatorio diferente.

Presione STEP (para un paso) o GO para ejecutar el algoritmo de optimización del enjambre de partículas.

El control deslizante LANDSCAPE-SMOOTHNESS determina cómo de suave queremos que sea el paisaje que se cree (cuanto más bajo más apreciables son los picos) cuando se presione el botón SETUP.

El control deslizante HILLS-NUMBER determina cuántas casillas (parcelas) recibirán una gran cantidad de elevación para simular colinas en nuestro espacio de búsqueda aleatorio.

El control deslizante POPULATION-SIZE controla el número de partículas utilizadas.

El control deslizante ATTRACTION-TO-PERSONAL-BEST determina la fuerza de atracción de cada partícula hacia la ubicación donde anteriormente había encontrado el valor más alto (en su propia historia).

El control deslizante ATTRACTION-TO-GLOBAL-BEST determina la fuerza de atracción de cada partícula hacia la mejor ubicación jamás descubierta por cualquier miembro del enjambre.

El control deslizante PARTICLE-INERTIA controla la cantidad en la que las partículas se mueven en la misma dirección en que lo han hecho (en lugar de ser arrastradas por las fuerzas de atracción).

El control deslizante PARTICLE-SPEED-LIMIT controla la velocidad máxima de movimiento (en las direcciones xoy) para cada partícula.


## COSAS A TENER EN CUENTA

A veces, el enjambre no encuentra el mejor mínimo global y se debe a que se atascó en algún punto del espacio de búsqueda, esto puede deberse a un "mínimo local" y puede que nunca encuentre la mejor solución. Dependiendo de la configuración de los parámtros (como la inercia o atracción hacia la mejor solución) podemos intentar evitar que ocurra este efecto, aunque en algunos casos podría llegar a ser inevitable.

Hay otras variantes de técnicas de PSO que cambian dinámicamente los valores de estas atracciones a las mejores soluciones personales y globales (similar a la 'temperatura' en el método de enfriamiento simulado) y permite crear una fuerza repulsiva para mantenerlas esparcidas en el espacio, y evitar quedarse atascado en una ubicación subóptima.


## COSAS PARA PROBAR

Intente modificar el control deslizante PARTICLE-INERTIA. Cuando el valor es 0.0, podemos ver que las partículas se mueven según la ubicación de sus mejores posiciones personales y las mejores posiciones globales.
Cuando el valor es 1.0, las velocidades de la partícula nunca cambian.


## MODELOS RELACIONADOS

Simple Genetic Algorithm, Artificial Neural Net, Perceptron, Hill Climbing Example (Code Example).


## CRÉDITOS

Este modelo es una implementación de un algoritmo de optimización por enjambre de partículas, del inglés 'Particle Swarm Optimization' (PSO) genérico y con una visualización en 3D para una mejor comprensión del mismo.

Forma parte del proyecto fin de grado (TFG) de Jose A. Martín Melguizo, dirigido por Rocio Romero Zaliz en la Universidad de Granada (UGR)

Granada, 15 de mayo de 2021

This work is licensed under the Creative Commons Attribution-NonCommercial-ShareAlike 4.0 License.  To view a copy of this license, visit https://creativecommons.org/licenses/by-nc-sa/4.0/ or send a letter to Creative Commons, 559 Nathan Abbott Way, Stanford, California 94305, USA.

![CC BY-NC-SA 4.0](https://licensebuttons.net/l/by-nc-sa/3.0/88x31.png)

## REFERENCIAS

* Based on the algorithm presented in the following paper: Kennedy, J. & Eberhart, R. (1995), 'Particle swarm optimization', Neural Networks, 1995. Proceedings., IEEE International Conference on 4.

* Stonedahl, F. and Wilensky, U. (2008).  NetLogo Particle Swarm Optimization model.  http://ccl.northwestern.edu/netlogo/models/ParticleSwarmOptimization.  Center for Connected Learning and Computer-Based Modeling, Northwestern University, Evanston, IL.
@#$#@#$#@
default
true
0
Polygon -7500403 true true 150 5 40 250 150 205 260 250

airplane
true
0
Polygon -7500403 true true 150 0 135 15 120 60 120 105 15 165 15 195 120 180 135 240 105 270 120 285 150 270 180 285 210 270 165 240 180 180 285 195 285 165 180 105 180 60 165 15

arrow
true
0
Polygon -7500403 true true 150 0 0 150 105 150 105 293 195 293 195 150 300 150

box
false
0
Polygon -7500403 true true 150 285 285 225 285 75 150 135
Polygon -7500403 true true 150 135 15 75 150 15 285 75
Polygon -7500403 true true 15 75 15 225 150 285 150 135
Line -16777216 false 150 285 150 135
Line -16777216 false 150 135 15 75
Line -16777216 false 150 135 285 75

bug
true
0
Circle -7500403 true true 96 182 108
Circle -7500403 true true 110 127 80
Circle -7500403 true true 110 75 80
Line -7500403 true 150 100 80 30
Line -7500403 true 150 100 220 30

butterfly
true
0
Polygon -7500403 true true 150 165 209 199 225 225 225 255 195 270 165 255 150 240
Polygon -7500403 true true 150 165 89 198 75 225 75 255 105 270 135 255 150 240
Polygon -7500403 true true 139 148 100 105 55 90 25 90 10 105 10 135 25 180 40 195 85 194 139 163
Polygon -7500403 true true 162 150 200 105 245 90 275 90 290 105 290 135 275 180 260 195 215 195 162 165
Polygon -16777216 true false 150 255 135 225 120 150 135 120 150 105 165 120 180 150 165 225
Circle -16777216 true false 135 90 30
Line -16777216 false 150 105 195 60
Line -16777216 false 150 105 105 60

car
false
0
Polygon -7500403 true true 300 180 279 164 261 144 240 135 226 132 213 106 203 84 185 63 159 50 135 50 75 60 0 150 0 165 0 225 300 225 300 180
Circle -16777216 true false 180 180 90
Circle -16777216 true false 30 180 90
Polygon -16777216 true false 162 80 132 78 134 135 209 135 194 105 189 96 180 89
Circle -7500403 true true 47 195 58
Circle -7500403 true true 195 195 58

circle
false
0
Circle -7500403 true true 0 0 300

circle 2
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240

cow
false
0
Polygon -7500403 true true 200 193 197 249 179 249 177 196 166 187 140 189 93 191 78 179 72 211 49 209 48 181 37 149 25 120 25 89 45 72 103 84 179 75 198 76 252 64 272 81 293 103 285 121 255 121 242 118 224 167
Polygon -7500403 true true 73 210 86 251 62 249 48 208
Polygon -7500403 true true 25 114 16 195 9 204 23 213 25 200 39 123

cylinder
false
0
Circle -7500403 true true 0 0 300

dot
false
0
Circle -7500403 true true 90 90 120

face happy
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 255 90 239 62 213 47 191 67 179 90 203 109 218 150 225 192 218 210 203 227 181 251 194 236 217 212 240

face neutral
false
0
Circle -7500403 true true 8 7 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Rectangle -16777216 true false 60 195 240 225

face sad
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 168 90 184 62 210 47 232 67 244 90 220 109 205 150 198 192 205 210 220 227 242 251 229 236 206 212 183

fish
false
0
Polygon -1 true false 44 131 21 87 15 86 0 120 15 150 0 180 13 214 20 212 45 166
Polygon -1 true false 135 195 119 235 95 218 76 210 46 204 60 165
Polygon -1 true false 75 45 83 77 71 103 86 114 166 78 135 60
Polygon -7500403 true true 30 136 151 77 226 81 280 119 292 146 292 160 287 170 270 195 195 210 151 212 30 166
Circle -16777216 true false 215 106 30

flag
false
0
Rectangle -7500403 true true 60 15 75 300
Polygon -7500403 true true 90 150 270 90 90 30
Line -7500403 true 75 135 90 135
Line -7500403 true 75 45 90 45

flower
false
0
Polygon -10899396 true false 135 120 165 165 180 210 180 240 150 300 165 300 195 240 195 195 165 135
Circle -7500403 true true 85 132 38
Circle -7500403 true true 130 147 38
Circle -7500403 true true 192 85 38
Circle -7500403 true true 85 40 38
Circle -7500403 true true 177 40 38
Circle -7500403 true true 177 132 38
Circle -7500403 true true 70 85 38
Circle -7500403 true true 130 25 38
Circle -7500403 true true 96 51 108
Circle -16777216 true false 113 68 74
Polygon -10899396 true false 189 233 219 188 249 173 279 188 234 218
Polygon -10899396 true false 180 255 150 210 105 210 75 240 135 240

house
false
0
Rectangle -7500403 true true 45 120 255 285
Rectangle -16777216 true false 120 210 180 285
Polygon -7500403 true true 15 120 150 15 285 120
Line -16777216 false 30 120 270 120

leaf
false
0
Polygon -7500403 true true 150 210 135 195 120 210 60 210 30 195 60 180 60 165 15 135 30 120 15 105 40 104 45 90 60 90 90 105 105 120 120 120 105 60 120 60 135 30 150 15 165 30 180 60 195 60 180 120 195 120 210 105 240 90 255 90 263 104 285 105 270 120 285 135 240 165 240 180 270 195 240 210 180 210 165 195
Polygon -7500403 true true 135 195 135 240 120 255 105 255 105 285 135 285 165 240 165 195

line
true
0
Line -7500403 true 150 0 150 300

line half
true
0
Line -7500403 true 150 0 150 150

pentagon
false
0
Polygon -7500403 true true 150 15 15 120 60 285 240 285 285 120

person
false
0
Circle -7500403 true true 110 5 80
Polygon -7500403 true true 105 90 120 195 90 285 105 300 135 300 150 225 165 300 195 300 210 285 180 195 195 90
Rectangle -7500403 true true 127 79 172 94
Polygon -7500403 true true 195 90 240 150 225 180 165 105
Polygon -7500403 true true 105 90 60 150 75 180 135 105

plant
false
0
Rectangle -7500403 true true 135 90 165 300
Polygon -7500403 true true 135 255 90 210 45 195 75 255 135 285
Polygon -7500403 true true 165 255 210 210 255 195 225 255 165 285
Polygon -7500403 true true 135 180 90 135 45 120 75 180 135 210
Polygon -7500403 true true 165 180 165 210 225 180 255 120 210 135
Polygon -7500403 true true 135 105 90 60 45 45 75 105 135 135
Polygon -7500403 true true 165 105 165 135 225 105 255 45 210 60
Polygon -7500403 true true 135 90 120 45 150 15 180 45 165 90

sheep
false
15
Circle -1 true true 203 65 88
Circle -1 true true 70 65 162
Circle -1 true true 150 105 120
Polygon -7500403 true false 218 120 240 165 255 165 278 120
Circle -7500403 true false 214 72 67
Rectangle -1 true true 164 223 179 298
Polygon -1 true true 45 285 30 285 30 240 15 195 45 210
Circle -1 true true 3 83 150
Rectangle -1 true true 65 221 80 296
Polygon -1 true true 195 285 210 285 210 240 240 210 195 210
Polygon -7500403 true false 276 85 285 105 302 99 294 83
Polygon -7500403 true false 219 85 210 105 193 99 201 83

square
false
0
Rectangle -7500403 true true 30 30 270 270

square 2
false
0
Rectangle -7500403 true true 30 30 270 270
Rectangle -16777216 true false 60 60 240 240

star
false
0
Polygon -7500403 true true 151 1 185 108 298 108 207 175 242 282 151 216 59 282 94 175 3 108 116 108

target
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240
Circle -7500403 true true 60 60 180
Circle -16777216 true false 90 90 120
Circle -7500403 true true 120 120 60

tree
false
0
Circle -7500403 true true 118 3 94
Rectangle -6459832 true false 120 195 180 300
Circle -7500403 true true 65 21 108
Circle -7500403 true true 116 41 127
Circle -7500403 true true 45 90 120
Circle -7500403 true true 104 74 152

triangle
false
0
Polygon -7500403 true true 150 30 15 255 285 255

triangle 2
false
0
Polygon -7500403 true true 150 30 15 255 285 255
Polygon -16777216 true false 151 99 225 223 75 224

truck
false
0
Rectangle -7500403 true true 4 45 195 187
Polygon -7500403 true true 296 193 296 150 259 134 244 104 208 104 207 194
Rectangle -1 true false 195 60 195 105
Polygon -16777216 true false 238 112 252 141 219 141 218 112
Circle -16777216 true false 234 174 42
Rectangle -7500403 true true 181 185 214 194
Circle -16777216 true false 144 174 42
Circle -16777216 true false 24 174 42
Circle -7500403 false true 24 174 42
Circle -7500403 false true 144 174 42
Circle -7500403 false true 234 174 42

turtle
true
0
Polygon -10899396 true false 215 204 240 233 246 254 228 266 215 252 193 210
Polygon -10899396 true false 195 90 225 75 245 75 260 89 269 108 261 124 240 105 225 105 210 105
Polygon -10899396 true false 105 90 75 75 55 75 40 89 31 108 39 124 60 105 75 105 90 105
Polygon -10899396 true false 132 85 134 64 107 51 108 17 150 2 192 18 192 52 169 65 172 87
Polygon -10899396 true false 85 204 60 233 54 254 72 266 85 252 107 210
Polygon -7500403 true true 119 75 179 75 209 101 224 135 220 225 175 261 128 261 81 224 74 135 88 99

wheel
false
0
Circle -7500403 true true 3 3 294
Circle -16777216 true false 30 30 240
Line -7500403 true 150 285 150 15
Line -7500403 true 15 150 285 150
Circle -7500403 true true 120 120 60
Line -7500403 true 216 40 79 269
Line -7500403 true 40 84 269 221
Line -7500403 true 40 216 269 79
Line -7500403 true 84 40 221 269

wolf
false
0
Polygon -16777216 true false 253 133 245 131 245 133
Polygon -7500403 true true 2 194 13 197 30 191 38 193 38 205 20 226 20 257 27 265 38 266 40 260 31 253 31 230 60 206 68 198 75 209 66 228 65 243 82 261 84 268 100 267 103 261 77 239 79 231 100 207 98 196 119 201 143 202 160 195 166 210 172 213 173 238 167 251 160 248 154 265 169 264 178 247 186 240 198 260 200 271 217 271 219 262 207 258 195 230 192 198 210 184 227 164 242 144 259 145 284 151 277 141 293 140 299 134 297 127 273 119 270 105
Polygon -7500403 true true -1 195 14 180 36 166 40 153 53 140 82 131 134 133 159 126 188 115 227 108 236 102 238 98 268 86 269 92 281 87 269 103 269 113

x
false
0
Polygon -7500403 true true 270 75 225 30 30 225 75 270
Polygon -7500403 true true 30 75 75 30 270 225 225 270
@#$#@#$#@
NetLogo 3D 6.2.0
@#$#@#$#@
need-to-manually-make-preview-for-this-model
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
default
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180
@#$#@#$#@
0
@#$#@#$#@
